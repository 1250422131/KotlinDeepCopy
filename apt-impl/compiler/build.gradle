plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.0'
}

group 'com.bennyhuo.kotlin'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlinx:kotlinx-metadata-jvm:0.0.4"
    compile "com.bennyhuo.aptutils:aptutils:1.7.1"
    compile 'com.squareup:kotlinpoet:1.2.0'

    compile project(":apt-impl:annotations")

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    from {
        fileTree(dir: 'libs', include: ['*.jar']).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

javadoc {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

//region bintray
apply plugin: 'com.novoda.bintray-release'

publish {
    userOrg = project.userName
    groupId = "com.bennyhuo.kotlin"
    repoName = project.repoName
    artifactId = 'deepcopy-compiler'
    publishVersion = project.publishVersion
    desc = project.desc//描述，不重要
    website = project.website
    bintrayUser = project.userName
    bintrayKey = project.apiKey
    dryRun = project.dryRun
}
//endregion